version = "1.0-SNAPSHOT"

description = 'Microservice :: Occasion'

configurations{
    // Used for copyDependency
    mongo {}
    providedCompile.extendsFrom(mongo)
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-collections4', version:'4.1'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
    providedCompile group: 'org.eclipse.microprofile.fault.tolerance', name: 'microprofile-fault-tolerance-api', version:'1.0-RC1'
    mongo group: 'org.mongodb', name: 'mongo-java-driver', version: '2.14.3'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.1"
    }
}

ext {
    warfileName = "microservice-occasion-" + version
    wlpServerName = "OccasionsServer"
}

war {
    archiveName = "${warfileName}.war"
}

liberty {
    server {
        name = wlpServerName
        configFile = new File("$projectDir/src/main/resources/liberty/config/server.xml")
        bootstrapProperties =
            ['app.name': war.archiveName, 'http.port': occasionHttpPort, 'https.port': occasionHttpsPort,
            'mongo.hostname': occasionMongoHostname, 'mongo.port': occasionMongoPort,
            'user.service.hostname': userHostname, 'user.service.port': userHttpsPort,
            'group.service.hostname': groupHostname, 'group.service.port': groupHttpsPort,
            'notification.service.hostname': notificationHostname, 'notification.service.port': notificationHttpPort,
            'notification_1_1.service.hostname': notification_v1_1_Hostname,
            'notification_1_1.service.port': notification_v1_1_HttpPort,
            'jwt.issuer': jwtIssuer]
        looseApplication = false
    }
}

task url {
    doLast { println occasionURL + '/occasions'}
}

test {
    group 'Verification'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-reports/unit")
    include '**/unit_test/**'

    systemProperties = ['mongo.test.port': occasionMongoPort]

    // run every time when called
    outputs.upToDateWhen { false }
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-reports/it")
    include '**/occasions_test/**'

    systemProperties = ['java.util.logging.config.file': "$buildDir/resources/logging.properties",
                        'liberty.test.occasion.service.url': occasionServiceURL,
                        'mongo.test.hostname': occasionMongoHostname,
                        'mongo.test.port': occasionMongoPort,
                        'jwt.issuer': jwtIssuer,
                        'javax.net.ssl.keyStorePassword': "secret"]

    // run every time when called
    outputs.upToDateWhen { false }
}

task testLoggingProperties(type: Copy) {
    from "$projectDir/src/test/resources"
    into "$buildDir/resources"
    outputs.file("$buildDir/resources/logging.properties")
}

task copyKeystore (type: Copy) {
    configurations.keystore.each {
        from(zipTree(it)) {
            include "keystore.jceks"
        }
    }
    into "$buildDir/wlp/usr/servers/${wlpServerName}/resources/security"
    outputs.file("$buildDir/wlp/usr/servers/${wlpServerName}/resources/security/keystore.jceks")
}

task testKeystore(type: Copy) {
    dependsOn 'copyKeystore'
    from "$buildDir/wlp/usr/servers/${wlpServerName}/resources/security"
    into "$buildDir/resources/test/truststore/"
    outputs.file("$buildDir/resources/test/truststore/keystore.jceks")
}

task copyDependency (type: Copy) {
    dependsOn 'libertyCreate'
    from configurations.mongo
    rename { "mongo-java-driver.jar" }
    into "${buildDir}/wlp/usr/shared/resources/"
    outputs.file("${buildDir}/wlp/usr/shared/resources/mongo-java-driver.jar")
}

task startDatabase (type: Exec) {
    group 'database'
    workingDir = "${projectDir}"
    dependsOn 'libertyCreate'
    description 'Execute the script to start the database.'

    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'cmd', '/C', '../run-app/startMongo.bat', 'occasionDB', occasionMongoPort, 'build', mongoPath
    } else {
        commandLine 'sh', '../run-app/startMongo.sh', 'occasionDB', occasionMongoPort, 'build', mongoPath
    }
    outputs.file("${buildDir}/occasionDB/occasionDB.pid")
    ignoreExitValue = true
}

task stopDatabase (type: Exec) {
    group 'database'
    workingDir = "${projectDir}"
    dependsOn 'libertyCreate'
    description 'Execute the script to stop the database.'
    args = ['groupDB', mongoPath]

    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'cmd', '/C', '../run-app/stopMongo.bat', 'occasionDB', 'build', mongoPath
    } else {
        commandLine 'sh', '../run-app/stopMongo.sh', 'occasionDB', 'build', mongoPath
    }
    ignoreExitValue = true
}

check.dependsOn 'integrationTest'
integrationTest.dependsOn 'libertyStart', 'testKeystore', 'testLoggingProperties'
integrationTest.finalizedBy 'libertyStop'
libertyStart.dependsOn 'copyKeystore', 'copyDependency', 'startDatabase'
libertyStop.finalizedBy 'stopDatabase'
clean.dependsOn 'libertyStop'
copyKeystore.dependsOn ':shared-keystore:assemble', 'libertyCreate'
