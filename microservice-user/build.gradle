apply plugin: 'war'
apply plugin: 'liberty'

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'Microservice :: User'

configurations{
    // Used for copyDependency
    mongo {}
}

dependencies {
    compile group: 'org.twitter4j', name: 'twitter4j-core', version:'4.0.6'
    testCompile project(':shared-keystore')
    testCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.1'
    testCompile group: 'org.springframework', name: 'spring-context', version:'4.3.9.RELEASE'
    providedCompile group: 'javax', name: 'javaee-api', version:'7.0'
    providedCompile group: 'com.ibm.websphere.appserver.api', name: 'com.ibm.websphere.appserver.api.jwt', version:'1.0.16'
    providedCompile group: 'org.eclipse.microprofile.jwt', name: 'microprofile-jwt-auth-api', version:'1.0-RC10'
    providedCompile group: 'org.eclipse.microprofile.config', name: 'microprofile-config-api', version:'1.0'
    libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version:'17.0.0.3'
    mongo group: 'org.mongodb', name: 'mongo-java-driver', version: '2.14.3'
}

buildscript {
    repositories {
        mavenCentral()
        // to be removed
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        // snapshot to removed upon release
        classpath "net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.0.2-SNAPSHOT"
    }
}

ext {
    warfileName = 'userMicroservice'
    wlpServerName = 'userServer'
}

war {
    archiveName = warfileName + '.' + extension
}

liberty {
    server {
        name = wlpServerName
        apps = [war]
        bootstrapProperties =
            ['app.name':war.archiveName, 'http.port':userHttpPort, 'https.port':userHttpsPort,
            'mongo.hostname':userMongoHostname, 'mongo.port':userMongoPort, 'twitter.consumer.key':twitterAppConsumerKey,
            'twitter.consumer.secret':twitterAppConsumerKey, 'acme.gifts.frontend.url':frontendURL,
            'jwt.issuer':jwtIssuer]

        // temp?
        looseApplication = false
    }
}

task url {
    doLast { println userURL }
}

task startDatabase (type: Exec) {
    description 'Execute the script to start the database.'

    // NOTE: these params are likely to change, especially 'build'
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'cmd', '/C', '../run-app/startMongo.bat', 'userDB', userMongoPort, 'build', mongoPath
    } else {
        commandLine 'sh', '../run-app/startMongo.sh', 'userDB', userMongoPort, 'build', mongoPath
    }

    doLast {
        println 'Info: Starting the database.'
    }
}

task stopDatabase (type: Exec) {
    description 'Execute the script to stop the database.'
    args = ['userDB', mongoPath]

    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'cmd', '/C', '../run-app/stopMongo.bat', 'userDB', 'build', mongoPath
    } else {
        commandLine 'sh', '../run-app/stopMongo.sh', 'userDB', 'build', mongoPath
    }

    doLast {
        println 'Info: Stopping the database.'
    }
}

test {
    doLast {
        println 'inside the test block'
    }
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-reports/unit")
    exclude '**/test/**'
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-reports/it")
    include '**/test/**'
    // include '$buildDir/test-classes'

    systemProperties = ['liberty.test.user.service.hostname': userHostname,
                        'liberty.test.port': userHttpPort,
                        'liberty.user.service.ssl.port': userHttpsPort,
                        'mongo.test.hostname': userMongoHostname,
                        'mongo.test.port': userMongoPort,
                        'liberty.test.user.service.url': userServiceURL,
                        'liberty.test.user.service.login.url': userServiceLoginURL]
}

task copyKeystore (type: Copy) {
//    temp fetching
    from '/Users/dshi/Desktop/sample-acmegifts/shared-keystore/target/shared-keystore-0.0.1-SNAPSHOT.jar'
    into "${buildDir}/wlp/usr/servers/${wlpServerName}/resources/security"
}

task copyDependency (type: Copy) {
    into "${buildDir}/wlp/usr/shared/resources/"
    from configurations.mongo
    dependsOn 'libertyCreate'
}

check.dependsOn integrationTest
integrationTest.dependsOn copyKeystore

startDatabase.mustRunAfter build
stopDatabase.mustRunAfter startDatabase

//integrationt est execution setup
//plugin to start and stop the databases
