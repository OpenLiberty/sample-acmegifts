apply plugin: 'war'
apply plugin: 'liberty'

sourceCompatibility = 1.8
targetCompatibility = 1.8

description = 'Microservice :: Authentication'

dependencies {
    testCompile group: 'org.glassfish', name: 'javax.json', version:'1.0.4'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version:'3.1.1'
    testCompile group: 'org.springframework', name: 'spring-context', version:'4.3.9.RELEASE'
    providedCompile group: 'javax', name: 'javaee-api', version:'7.0'
    providedCompile group: 'com.ibm.websphere.appserver.api', name: 'com.ibm.websphere.appserver.api.jwt', version:'1.0.16'
    libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version:'17.0.0.3'
    testCompile project(':shared-keystore')
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.1"
    }
}

ext {
    warfileName = 'authMicroservice'
    wlpServerName = 'authServer'
}

war {
    archiveName = "${warfileName}.${extension}"
}

liberty {
    server {
        name = wlpServerName
        apps = [war]
        bootstrapProperties =
            ['app.name':war.archiveName, 'http.port':authHttpPort, 'https.port':authHttpsPort,
            'jwt.issuer':jwtIssuer]
    }
}

task url {
    doLast { println authURL }
}

test {
    group 'Verification'
    reports.html.destination = file("$buildDir/reports/unit")
    reports.junitXml.destination = file("$buildDir/test-reports/unit")
    exclude '**/test/**'
}

task integrationTest(type: Test) {
    group 'Verification'
    description 'Runs the integration tests.'
    reports.html.destination = file("$buildDir/reports/it")
    reports.junitXml.destination = file("$buildDir/test-reports/it")
    include '**/test/**'

    systemProperties = ['liberty.test.auth.service.url': authServiceURL,
                        'javax.net.ssl.keyStorePassword': "secret"]
}

task copyKeystore(type: Copy) {
    configurations.testCompile.each {
        from(zipTree(it)) {
            include "keystore.jceks"
        }
    }
    into "${buildDir}/wlp/usr/servers/${wlpServerName}/resources/security"
    outputs.file("${buildDir}/wlp/usr/servers/${wlpServerName}/resources/security/keystore.jceks")
}

task testKeystore(type: Copy) {
    from "${buildDir}/wlp/usr/servers/${wlpServerName}/resources/security"
    into "${buildDir}/resources/test/truststore/"
    outputs.file("${buildDir}/resources/test/truststore/keystore.jceks")
}

integrationTest.dependsOn 'libertyStart', 'testKeystore'
integrationTest.finalizedBy 'libertyStop'
libertyStart.dependsOn 'copyKeystore'
copyKeystore.dependsOn ':shared-keystore:assemble', 'libertyCreate'
